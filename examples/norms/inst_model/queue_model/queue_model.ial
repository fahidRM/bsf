%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Queue institution for two types of general query:
% a) agent itself is a new arrival agent, then it is obliged to queue the last 
% b) agent detects some disable agent approaching, then it is obliged to yield
%
% Tingting Li,  8 May 2012
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


%%% institution queue 
institution queue;

%%% component 
type Agent;
type Type; 

%%% creation event
create event createQueue;

%%% exogenous event
exogenous event newArrival(Agent,Type);   % new agent arrives 
exogenous event detectDisable(Agent);     % agent detects some disable agent approaching 

exogenous event queueLast(Agent);   % event for agent to join the end of queue
exogenous event yield(Agent);      % event for agent to yield 
exogenous event deadline; 

%%% institutional events
inst event iniOblQueueLast(Agent,Type); 
inst event iniOblYield(Agent);

%%% violation event
violation event vioQueue(Agent); 
violation event vioYield(Agent); 

%%% fluents

%%% rule 1: for new arrival agent %%% 
newArrival(Agent,Type) generates iniOblQueueLast(Agent,Type); 
iniOblQueueLast(Agent,Type) initiates perm(queueLast(Agent)), obl(queueLast(Agent), deadline, vioQueue(Agent));

%%% rule 2: for agent who detects disable %%% 

detectDisable(Agent) generates iniOblYield(Agent);
iniOblYield(Agent) initiates perm(yield(Agent)),obl(yield(Agent), deadline, vioYield(Agent));

%%% initiate permissions and empowerment  
initially perm(deadline);
initially perm(iniOblQueueLast(Agent,Type)), pow(iniOblQueueLast(Agent,Type));
initially perm(iniOblYield(Agent)), pow(iniOblYield(Agent));
initially perm(newArrival(Agent,Type));
initially perm(detectDisable(Agent));